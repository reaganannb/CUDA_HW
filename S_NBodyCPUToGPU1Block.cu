// Name: Reagan Burleson
// Creating a GPU nBody simulation from an nBody CPU simulation. 
// nvcc 18NBodyGPU.cu -o temp -lglut -lm -lGLU -lGL

/*
 What to do:
 This is some lean nBody code that runs on the CPU. Rewrite it, keeping the same general format, 
 but offload the compute-intensive parts of the code to the GPU for acceleration.
 Note: The code takes two arguments as inputs:
 1. The number of bodies to simulate, (We will keep the number of bodies under 1024 for this HW so it can be run on one block.)
 2. Whether to draw sub-arrangements of the bodies during the simulation (1), or only the first and last arrangements (0).
*/

/*
 Purpose:
 To learn how to move an Nbody CPU simulation to an Nbody GPU simulation..
*/

// Include files
#include <GL/glut.h>
#include <math.h>
#include <stdio.h>
#include <stdlib.h>
#include <sys/time.h>
#include <cuda_runtime.h>
#include <device_launch_parameters.h>

// Defines
#define PI 3.14159265359
#define DRAW_RATE 10

// This is to create a Lennard-Jones type function G/(r^p) - H(r^q). (p < q) p has to be less than q.
// In this code we will keep it a p = 2 and q = 4 problem. The diameter of a body is found using the general
// case so it will be more robust but in the code leaving it as a set 2, 4 problem make the coding much easier.
#define G 10.0
#define H 10.0
#define LJP  2.0
#define LJQ  4.0

#define DT 0.0001
#define RUN_TIME 1.0

// Globals
int N, DrawFlag;
float3 *P, *V, *F;
float *M; 
float GlobeRadius, Diameter, Radius;
float Damp;

// Function prototypes
void keyPressed(unsigned char, int, int);
long elaspedTime(struct timeval, struct timeval);
void drawPicture();
void timer();
void setup();
void nBody();
int main(int, char**);

__global__ void zeroForces(float3* F, int N)
{
    int i = threadIdx.x;
    if (i < N) { F[i].x = 0.f; F[i].y = 0.f; F[i].z = 0.f; }
}

void keyPressed(unsigned char key, int x, int y)
{
	if(key == 's')
	{
		timer();
	}
	
	if(key == 'q')
	{
		exit(0);
	}
}
//Each thread i accumulates total force on body i from all j != i
__global__ void computeForces(const float3* __restrict__ P,
                              const float*  __restrict__ M,
                              float3*       __restrict__ F,
                              int N, float softening)
{
    int i = threadIdx.x;
    if (i >= N) return;

    float3 Pi = P[i];
    float  Mi = M[i];
    float3 Fi = make_float3(0.f, 0.f, 0.f);

    for (int j = 0; j < N; ++j)
    {
        if (j == i) continue;
        float dx = P[j].x - Pi.x;
        float dy = P[j].y - Pi.y;
        float dz = P[j].z - Pi.z;
        float d2 = dx*dx + dy*dy + dz*dz + softening; //soften to avoid div by zero
        float d  = sqrtf(d2);

        //Lennard-Jones-like:  G Mi Mj / r^2  -  H Mi Mj / r^4
        float common = (G*Mi*M[j])/d2 - (H*Mi*M[j])/(d2*d2);
        float invd   = 1.0f / d;
        Fi.x += common * dx * invd;
        Fi.y += common * dy * invd;
        Fi.z += common * dz * invd;
    }
    F[i] = Fi;
}

__global__ void integrate(float3* P, float3* V, const float3* F, const float* M,
                          float dt, float damp, int N, int firstStep)
{
    int i = threadIdx.x;
    if (i >= N) return;

    float invMi = 1.0f / M[i];
    if (firstStep)
    {
        V[i].x += (F[i].x * invMi) * 0.5f * dt;
        V[i].y += (F[i].y * invMi) * 0.5f * dt;
        V[i].z += (F[i].z * invMi) * 0.5f * dt;
    }
    else
    {
        V[i].x += ((F[i].x - damp*V[i].x) * invMi) * dt;
        V[i].y += ((F[i].y - damp*V[i].y) * invMi) * dt;
        V[i].z += ((F[i].z - damp*V[i].z) * invMi) * dt;
    }

    P[i].x += V[i].x * dt;
    P[i].y += V[i].y * dt;
    P[i].z += V[i].z * dt;
}

// Calculating elasped time.
long elaspedTime(struct timeval start, struct timeval end)
{
	// tv_sec = number of seconds past the Unix epoch 01/01/1970
	// tv_usec = number of microseconds past the current second.
	
	long startTime = start.tv_sec * 1000000 + start.tv_usec; // In microseconds.
	long endTime = end.tv_sec * 1000000 + end.tv_usec; // In microseconds

	// Returning the total time elasped in microseconds
	return endTime - startTime;
}

void drawPicture()
{
	int i;
	
	glClear(GL_COLOR_BUFFER_BIT);
	glClear(GL_DEPTH_BUFFER_BIT);
	
	glColor3d(1.0,1.0,0.5);
	for(i=0; i<N; i++)
	{
		glPushMatrix();
		glTranslatef(P[i].x, P[i].y, P[i].z);
		glutSolidSphere(Radius,20,20);
		glPopMatrix();
	}
	
	glutSwapBuffers();
}

void timer()
{	
	timeval start, end;
	long computeTime;
	
	drawPicture();
	gettimeofday(&start, NULL);
    		nBody();
    	gettimeofday(&end, NULL);
    	drawPicture();
    	
	computeTime = elaspedTime(start, end);
	printf("\n The compute time was %ld microseconds.\n\n", computeTime);
}

void setup()
{
	float randomAngle1, randomAngle2, randomRadius;
	float d, dx, dy, dz;
	int test;

	Damp = 0.5;

	//unified alloc. so cpu and gpu share
	cudaMallocManaged(&M, N*sizeof(float));
    cudaMallocManaged(&P, N*sizeof(float3));
    cudaMallocManaged(&V, N*sizeof(float3));
    cudaMallocManaged(&F, N*sizeof(float3));
    	
	
	Diameter = pow(H/G, 1.0/(LJQ - LJP)); // This is the value where the force is zero for the L-J type force.
	Radius = Diameter/2.0;
	
	// Using the radius of a body and a 68% packing ratio to find the radius of a global sphere that should hold all the bodies.
	// Then we double this radius just so we can get all the bodies setup with no problems. 
	float totalVolume = float(N)*(4.0/3.0)*PI*Radius*Radius*Radius;
	totalVolume /= 0.68;
	float totalRadius = pow(3.0*totalVolume/(4.0*PI), 1.0/3.0);
	GlobeRadius = 2.0*totalRadius;
	
	// Randomly setting these bodies in the glaobal sphere and setting the initial velosity, inotial force, and mass.
	for(int i = 0; i < N; i++)
	{
		test = 0;
		while(test == 0)
		{
			// Get random position.
			randomAngle1 = ((float)rand()/(float)RAND_MAX)*2.0*PI;
			randomAngle2 = ((float)rand()/(float)RAND_MAX)*PI;
			randomRadius = ((float)rand()/(float)RAND_MAX)*GlobeRadius;
			P[i].x = randomRadius*cos(randomAngle1)*sin(randomAngle2);
			P[i].y = randomRadius*sin(randomAngle1)*sin(randomAngle2);
			P[i].z = randomRadius*cos(randomAngle2);
			
			// Making sure the balls centers are at least a diameter apart.
			// If they are not throw these positions away and try again.
			test = 1;
			for(int j = 0; j < i; j++)
			{
				dx = P[i].x-P[j].x;
				dy = P[i].y-P[j].y;
				dz = P[i].z-P[j].z;
				d = sqrt(dx*dx + dy*dy + dz*dz);
				if(d < Diameter)
				{
					test = 0;
					break;
				}
			}
		}
	
		V[i].x = 0.0;
		V[i].y = 0.0;
		V[i].z = 0.0;
		
		F[i].x = 0.0;
		F[i].y = 0.0;
		F[i].z = 0.0;
		
		M[i] = 1.0;
	}
	printf("\n To start timing type s.\n");
}

//gpu nbody
void nBody()
{
	int    drawCount = 0; 
	float  time = 0.0;
	float dt = 0.0001;

	dim3 grid(1), block(N);
    const float softening = 1e-6f;

	int firstStep = 1;

	while(time < RUN_TIME)
    {
        zeroForces<<<grid, block>>>(F, N);
        computeForces<<<grid, block>>>(P, M, F, N, softening);
        integrate<<<grid, block>>>(P, V, F, M, dt, Damp, N, firstStep);
        firstStep = 0;

        //Draw cadence
        if(drawCount == DRAW_RATE)
        {
            if(DrawFlag) {
                cudaDeviceSynchronize();
                drawPicture();
            }
            drawCount = 0;
        }

        time += dt;
        drawCount++;
    }

    //Final sync so the caller sees last positions immediately
    cudaDeviceSynchronize();
}

int main(int argc, char** argv)
{
	if( argc < 3)
	{
		printf("\n You need to enter the number of bodies (an int)"); 
		printf("\n and if you want to draw the bodies as they move (1 draw, 0 don't draw),");
		printf("\n on the comand line.\n"); 
		exit(0);
	}
	else
	{
		N = atoi(argv[1]);
		DrawFlag = atoi(argv[2]);
	}
	
	setup();
	
	int XWindowSize = 1000;
	int YWindowSize = 1000;
	
	glutInit(&argc,argv);
	glutInitDisplayMode(GLUT_DOUBLE | GLUT_DEPTH | GLUT_RGB);
	glutInitWindowSize(XWindowSize,YWindowSize);
	glutInitWindowPosition(0,0);
	glutCreateWindow("nBody Test");
	GLfloat light_position[] = {1.0, 1.0, 1.0, 0.0};
	GLfloat light_ambient[]  = {0.0, 0.0, 0.0, 1.0};
	GLfloat light_diffuse[]  = {1.0, 1.0, 1.0, 1.0};
	GLfloat light_specular[] = {1.0, 1.0, 1.0, 1.0};
	GLfloat lmodel_ambient[] = {0.2, 0.2, 0.2, 1.0};
	GLfloat mat_specular[]   = {1.0, 1.0, 1.0, 1.0};
	GLfloat mat_shininess[]  = {10.0};
	glClearColor(0.0, 0.0, 0.0, 0.0);
	glShadeModel(GL_SMOOTH);
	glColorMaterial(GL_FRONT, GL_AMBIENT_AND_DIFFUSE);
	glLightfv(GL_LIGHT0, GL_POSITION, light_position);
	glLightfv(GL_LIGHT0, GL_AMBIENT, light_ambient);
	glLightfv(GL_LIGHT0, GL_DIFFUSE, light_diffuse);
	glLightfv(GL_LIGHT0, GL_SPECULAR, light_specular);
	glLightModelfv(GL_LIGHT_MODEL_AMBIENT, lmodel_ambient);
	glMaterialfv(GL_FRONT, GL_SPECULAR, mat_specular);
	glMaterialfv(GL_FRONT, GL_SHININESS, mat_shininess);
	glEnable(GL_LIGHTING);
	glEnable(GL_LIGHT0);
	glEnable(GL_COLOR_MATERIAL);
	glEnable(GL_DEPTH_TEST);
	glutKeyboardFunc(keyPressed);
	glutDisplayFunc(drawPicture);
	
	float3 eye = {0.0f, 0.0f, 2.0f*GlobeRadius};
	float near = 0.2;
	float far = 5.0*GlobeRadius;
	
	glMatrixMode(GL_PROJECTION);
	glLoadIdentity();
	glFrustum(-0.2, 0.2, -0.2, 0.2, near, far);
	glMatrixMode(GL_MODELVIEW);
	glPolygonMode(GL_FRONT_AND_BACK, GL_FILL);
	gluLookAt(eye.x, eye.y, eye.z, 0.0, 0.0, 0.0, 0.0, 1.0, 0.0);
	
	glutMainLoop();

	cudaFree(M); cudaFree(P); cudaFree(V); cudaFree(F);

	return 0;
}





